# Copyright (c) 2017-2021, Lawrence Livermore National Security, LLC and
# other Axom Project Developers. See the top-level LICENSE file for details.
#
# SPDX-License-Identifier: (BSD-3-Clause)
#------------------------------------------------------------------------------

cmake_minimum_required(VERSION 3.0 FATAL_ERROR)

@PACKAGE_INIT@

if(NOT AXOM_FOUND)

  #----------------------------------------------------------------------------
  # Set version and paths
  #----------------------------------------------------------------------------
  
  set(AXOM_VERSION       "@AXOM_VERSION_FULL@")
  set(AXOM_VERSION_MAJOR "@AXOM_VERSION_MAJOR@")
  set(AXOM_VERSION_MINOR "@AXOM_VERSION_MINOR@")
  set(AXOM_VERSION_PATCH "@AXOM_VERSION_PATCH@")
  
  set(AXOM_INSTALL_PREFIX "@AXOM_INSTALL_PREFIX@")
  set(AXOM_INCLUDE_DIRS "${AXOM_INSTALL_PREFIX}/include")

  #----------------------------------------------------------------------------
  # Set user configuration options and features
  #----------------------------------------------------------------------------

  # Language features
  set(AXOM_ENABLE_FORTRAN     "@ENABLE_FORTRAN@")
  set(AXOM_USE_CUDA           "@AXOM_USE_CUDA@")
  set(AXOM_USE_MPI            "@AXOM_USE_MPI@")
  set(AXOM_USE_OPENMP         "@AXOM_USE_OPENMP@")

  # Axom components
  set(AXOM_ENABLE_INLET       "@AXOM_ENABLE_INLET@")
  set(AXOM_ENABLE_LUMBERJACK  "@AXOM_ENABLE_LUMBERJACK@")
  set(AXOM_ENABLE_MINT        "@AXOM_ENABLE_MINT@")
  set(AXOM_ENABLE_PRIMAL      "@AXOM_ENABLE_PRIMAL@")
  set(AXOM_ENABLE_QUEST       "@AXOM_ENABLE_QUEST@")
  set(AXOM_ENABLE_SIDRE       "@AXOM_ENABLE_SIDRE@")
  set(AXOM_ENABLE_SLAM        "@AXOM_ENABLE_SLAM@")
  set(AXOM_ENABLE_SLIC        "@AXOM_ENABLE_SLIC@")
  set(AXOM_ENABLE_SPIN        "@AXOM_ENABLE_SPIN@")

  # Axom built-in TPLs
  set(AXOM_USE_CLI11          "@AXOM_USE_CLI11@")
  set(AXOM_USE_FMT            "@AXOM_USE_FMT@")
  set(AXOM_USE_SOL            "@AXOM_USE_SOL@")
  set(AXOM_USE_SPARSEHASH     "@AXOM_USE_SPARSEHASH@")

  # Axom external TPLs
  set(AXOM_USE_C2C            "@AXOM_USE_C2C@")
  set(AXOM_USE_CONDUIT        "@AXOM_USE_CONDUIT@")
  set(AXOM_USE_HDF5           "@AXOM_USE_HDF5@")
  set(AXOM_USE_LUA            "@AXOM_USE_LUA@")
  set(AXOM_USE_MFEM           "@AXOM_USE_MFEM@")
  set(AXOM_USE_RAJA           "@AXOM_USE_RAJA@")
  set(AXOM_USE_SCR            "@AXOM_USE_SCR@")
  set(AXOM_USE_UMPIRE         "@AXOM_USE_UMPIRE@")

  #----------------------------------------------------------------------------
  # Bring in required  dependencies for this axom configuration
  #----------------------------------------------------------------------------
  include(CMakeFindDependencyMacro)

  # c2c
  if(AXOM_USE_C2C)
    set(AXOM_C2C_DIR     "@C2C_DIR@")
    # Note: Targets not currently imported
  endif()

  # conduit
  if(AXOM_USE_CONDUIT)
    set(AXOM_CONDUIT_DIR  "@CONDUIT_DIR@")
    if(NOT CONDUIT_DIR) 
      set(CONDUIT_DIR ${AXOM_CONDUIT_DIR}) 
    endif()
    find_dependency(Conduit REQUIRED NO_DEFAULT_PATH PATHS "${CONDUIT_DIR}/lib/cmake")
  endif()

  # hdf5
  if(AXOM_USE_HDF5)
    set(AXOM_HDF5_DIR     "@HDF5_DIR@")
    # Note: Targets not currently imported
  endif()

  # lua
  if(AXOM_USE_LUA)
    set(AXOM_LUA_DIR     "@LUA_DIR@")
    # Note: Targets not currently imported
  endif()

  # mfem
  if(AXOM_USE_MFEM)
    set(AXOM_MFEM_DIR     "@MFEM_DIR@")
    # Note: Targets not currently imported
  endif()

  # raja
  if(AXOM_USE_RAJA)
    set(AXOM_RAJA_DIR     "@RAJA_DIR@")
    if(NOT RAJA_DIR) 
      set(RAJA_DIR ${AXOM_RAJA_DIR}) 
    endif()
    find_dependency(RAJA REQUIRED NO_DEFAULT_PATH PATHS "${RAJA_DIR}/share/raja/cmake")
  endif()

  # scr
  if(AXOM_USE_SCR)
    set(AXOM_SCR_DIR      "@SCR_DIR@")
    # Note: Targets not currently imported
  endif()

  # umpire
  if(AXOM_USE_UMPIRE)
    set(AXOM_UMPIRE_DIR   "@UMPIRE_DIR@")
    if(NOT UMPIRE_DIR) 
      set(UMPIRE_DIR ${AXOM_UMPIRE_DIR}) 
    endif()
    find_dependency(umpire REQUIRED NO_DEFAULT_PATH PATHS "${UMPIRE_DIR}/share/umpire/cmake")
  endif()

  #----------------------------------------------------------------------------
  # Include targets exported by cmake
  #----------------------------------------------------------------------------
  get_filename_component(AXOM_CMAKE_CONFIG_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)
  include(${AXOM_CMAKE_CONFIG_DIR}/axom-targets.cmake)

  # HACK: Clear INTERFACE_COMPILE_OPTIONS to avoid requiring OpenMP in user code
  # This can (hopefully) be removed when we fix axom/blt's treatment of OpenMP
  if(AXOM_USE_OPENMP)
    set(_fix_tgts axom)
    if(AXOM_USE_RAJA)
      list(APPEND _fix_tgts RAJA)
    endif()
    set_target_properties(${_fix_tgts} PROPERTIES INTERFACE_COMPILE_OPTIONS "")
    unset(_fix_tgts)
  endif()

  #----------------------------------------------------------------------------
  # Indicate that axom is correctly set up
  #----------------------------------------------------------------------------
  set(AXOM_FOUND TRUE)

endif()

